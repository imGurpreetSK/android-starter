apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

apply from: "../gradle/dependencies.gradle"

android {
  compileSdkVersion 28

  defaultConfig {
    applicationId "com.gurpreetsk.android_starter"
    minSdkVersion versions.minSdkVersion
    targetSdkVersion versions.targetSdkVersion
    multiDexEnabled true
    versionCode 1
    versionName "0.0.1"

    vectorDrawables.useSupportLibrary = true

    buildConfigField "String", "BASE_URL", "\"\"" // TODO(gs) - update base url.
    buildConfigField "int", "DB_VERSION", "1"

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/db-schemas".toString()]
      }
    }

    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    debug {
      debuggable true
      versionNameSuffix "-debug"
      applicationIdSuffix ".debug"

      minifyEnabled false
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

      buildConfigField "String", "DB_NAME", "\"starter-db-debug\""
    }

    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"

      buildConfigField "String", "DB_NAME", "\"starter-db\""
    }
  }

  sourceSets {
    main.java.srcDirs        += "src/main/kotlin"
    debug.java.srcDirs       += "src/debug/kotlin"
    test.java.srcDirs        += "src/test/kotlin"
    androidTest.java.srcDirs += "src/androidTest/kotlin"
  }
}

android {
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

androidExtensions {
  experimental = true
}

kapt {
  generateStubs = true
}

repositories {
  jcenter()
}

configurations {
  ktlint
}

dependencies {
  implementation fileTree(dir: "libs", include: ["*.jar"])
  ktlint "com.github.shyiko:ktlint:0.31.0"

  // Android KTX
  implementation deps.androidKtx

  // Coroutines
  // TODO: Remove if not needed.
  implementation deps.coroutines.core
  implementation deps.coroutines.android

  // Chuck
  debugImplementation   deps.chuck.operational
  releaseImplementation deps.chuck.noOp

  // Dagger 2
  implementation deps.dagger.core
  kapt           deps.dagger.compiler

  // Kotlin stdlib
  implementation deps.kotlinStdLib

  // Leak canary
  debugImplementation   deps.leakCanary.debug
  releaseImplementation deps.leakCanary.release

  // Logging
  implementation deps.timber

  // Moshi
  implementation deps.moshi.core
  implementation deps.moshi.adapters
  kapt           deps.moshi.compiler

  // okHttp
  implementation deps.okhttp.core
  implementation deps.okhttp.loggingInterceptor

  // Retrofit
  implementation deps.retrofit.core
  implementation deps.retrofit.rxjavaAdapter
  implementation deps.retrofit.moshiAdapter

  // Reactive Extensions
  implementation deps.rx.kotlin
  implementation deps.rx.java
  implementation deps.rx.android
  implementation deps.rx.relay
  implementation deps.rx.bindings

  // Room
  implementation deps.room.core
  implementation deps.room.rxAdapter
  kapt           deps.room.compiler

  // Stetho
  debugImplementation deps.stetho

  // Support
  implementation deps.support.appCompat
  implementation deps.support.material
  implementation deps.support.vectorDrawable
  implementation deps.support.constraintLayout

  /* -----  Testing  ----- */

  // JUnit testing
  testImplementation "junit:junit:4.12"
  testImplementation "com.google.truth:truth:1.0"
  testImplementation "org.robolectric:robolectric:4.2.1"

  // Android test
  androidTestImplementation "com.google.truth:truth:1.0"
  androidTestImplementation "androidx.test:runner:1.2.0"
  androidTestImplementation "androidx.test.ext:junit:1.1.1"
  androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"

  // Mocking
  testImplementation "org.mockito:mockito-core:2.28.2"
  testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
  androidTestImplementation "org.mockito:mockito-android:2.25.1"
  androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"

  // Misc
  testImplementation deps.room.testing
}

android {
  sourceSets {
    debug {
      manifest.srcFile "src/debug/AndroidManifest.xml"
    }
  }
}

apply from: "../gradle/ktlint.gradle"
